extends Control
@onready var musica = $AudioStreamPlayer2D
@onready var monete_label := $MoneteLabel
@onready var user := $PlayerInfo
@export var profile_textures: Array[Texture2D]
@onready var views = {
	"main": $MainButtons,
	"options": $Options,
	"armadietto": $Armadietto,
	"selection": $Gioca,
	"leaderboard": $LeaderboardMenu
}

func _ready() -> void:
	switch_view("main")
	musica.volume_db = 0 
	musica.play()
	# Aggiornamento Monete
	_update_monete_label()
	GameData.monete_aggiornate.connect(_on_monete_aggiornate_signal)

	# Aggiornamento Icona Profilo
	GameData.profile_icon_changed.connect(_update_player_icon)
	_update_player_icon() # Imposta subito l'icona salvata
	
func _update_monete_label():
	monete_label.text = ": %d" % GameData.monete_stella

func _on_monete_aggiornate_signal(_nuovo_valore):
	_update_monete_label()

func _update_player_icon():
	if GameData.current_icon_index < profile_textures.size():
		user.icon = profile_textures[GameData.current_icon_index]
	else:
		print("Errore: Indice icona non trovato nell'array!")

func switch_view(view_name: String) -> void:
	for key in views:
		views[key].visible = (key == view_name)
	# Gestione speciale per il tasto "Back" che non Ã¨ una vista intera
	$Options/VBoxContainer/Back.visible = (view_name != "main")

func fade_out_music(duration: float = 1.0):
	var tween = create_tween()
	tween.tween_property(musica, "volume_db", -80.0, duration)
	tween.tween_callback(musica.stop)

func _on_start_pressed(): switch_view("selection")
func _on_settings1_pressed(): switch_view("options")
func _on_settings2_pressed(): switch_view("armadietto")
func _on_back_pressed():	switch_view("main")
func _on_player_info_pressed(): switch_view("leaderboard")
func _on_mod_pressed(mod: String) -> void:
	fade_out_music(0.5) 
	FadeTransition.change_scene("res://scenes/Game/Game"+mod+".tscn")
